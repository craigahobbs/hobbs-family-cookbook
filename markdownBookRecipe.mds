# Licensed under the MIT License
# https://github.com/craigahobbs/hobbs-family-cookbook/blob/main/LICENSE

include <args.mds>
include 'markdownBookArgs.mds'


#
# Markdown Book scalable recipe library
#


# Recipe scaling min/max
recipeMinScale = 0.125
recipeMaxScale = 8


# Parse the Markdown Book application arguments
recipeArgs = argsParse(markdownBookArguments)


# Render the recipe page's top-menu
function recipeMenu():
    category = objectGet(recipeArgs, 'category')
    indexURL = '#url=&' + \
        stringSlice(argsURL(markdownBookArguments, objectNew('category', category), false, category), 1)
    markdownPrint('[Index](' + indexURL + ')')
endfunction


# Render the recipe page's header. Options: author, servings.
function recipeHeader(options):
    category = objectGet(recipeArgs, 'category')
    scale = objectGet(recipeArgs, 'scale')
    author = if(options != null, objectGet(options, 'author'))
    servings = if(options != null, objectGet(options, 'servings'))
    halfScale = 0.5 * scale
    doubleScale = 2 * scale
    if author != null:
        markdownPrint('', 'Author: ' + markdownEscape(author))
    endif
    markdownPrint( \
        '', 'Scale: ' + mathRound(scale, 3) + '&nbsp;&nbsp; ( ' + \
        if(halfScale < recipeMinScale, 'Halve', \
            argsLink(markdownBookArguments, 'Halve', objectNew('category', category, 'scale', mathRound(halfScale, 3)))), \
        ' | ', \
        if(doubleScale > recipeMaxScale, 'Double', \
            argsLink(markdownBookArguments, 'Double', objectNew('category', category, 'scale', mathRound(doubleScale, 3)))), \
        ')' \
    )
    if servings != null:
        markdownPrint('', 'Servings: ' + scale * servings)
    endif
endfunction


# Parse, scale, and render the recipe ingredient text
function recipeIngredients(ingredients):
    scale = objectGet(recipeArgs, 'scale')
    for ingredient in ingredients:
        if ingredient != null:
            ingredientModel = recipeParseIngredient(ingredient)
            if ingredientModel != null:
                markdownPrint('', recipeIngredientText(ingredientModel, scale))
            else:
                systemLogDebug('Invalid ingredient text "' + ingredient + '"')
            endif
        endif
    endfor
endfunction


# Parse ingredient text to an ingredient model
function recipeParseIngredient(ingredient):
    match = regexMatch(recipeIngredientRegex, ingredient)
    groups = if(match != null, objectGet(match, 'groups'), null)
    whole = if(match != null && objectGet(groups, 'whole') != null, numberParseFloat(objectGet(groups, 'whole')), 0)
    numer = if(match != null && objectGet(groups, 'numer') != null, numberParseInt(objectGet(groups, 'numer')), 0)
    denom = if(match != null && objectGet(groups, 'denom') != null, numberParseInt(objectGet(groups, 'denom')), 1)
    unit = if(match != null && objectGet(groups, 'unit') != null, objectGet(groups, 'unit'), null)
    name = if(match != null, objectGet(groups, 'name'), null)
    if name == null || (whole == 0 && numer == 0):
        return null
    endif

    # Normalize the unit
    if unit == null:
        unit = 'count'
    elif objectHas(recipeAlternateUnits, unit):
        unit = objectGet(recipeAlternateUnits, unit)
    endif

    return objectNew( \
        'amount', whole + numer / denom, \
        'unit', unit, \
        'name', name \
    )
endfunction


# Ingredient unit info map
recipeUnitInfo = objectNew( \
    'count', objectNew( \
        'baseUnit', 'count', \
        'display', '', \
        'baseRatio', 1, \
        'fractions', arrayNew(2, 4, 8) \
    ), \
    'cup', objectNew( \
        'alternates', arrayNew('C', 'Cup', 'Cups', 'c', 'cups'), \
        'display', 'C', \
        'baseUnit', 'tsp', \
        'baseRatio', 48, \
        'fractions', arrayNew(2, 3, 4, 8) \
    ), \
    'lb', objectNew( \
        'alternates', arrayNew('Lb', 'Lbs', 'Pound', 'Pounds', 'lbs', 'pound', 'pounds'), \
        'baseUnit', 'oz', \
        'baseRatio', 16, \
        'fractions', arrayNew(2, 4, 8) \
    ), \
    'oz', objectNew( \
        'alternates', arrayNew('Oz', 'Ounce', 'Ounces', 'ounce', 'ounces'), \
        'baseUnit', 'oz', \
        'baseRatio', 1, \
        'fractions', arrayNew(2, 4, 8) \
    ), \
    'pinch', objectNew( \
        'baseUnit', 'pinch', \
        'baseRatio', 1, \
        'fractions', arrayNew(1) \
    ), \
    'tbsp', objectNew( \
        'alternates', arrayNew('T', 'Tbsp', 'Tablespoon', 'Tablespoons', 'tablespoon', 'tablespoons'), \
        'baseUnit', 'tsp', \
        'baseRatio', 3, \
        'fractions', arrayNew(1, 2) \
    ), \
    'tsp', objectNew( \
        'alternates', arrayNew('Tsp', 'Teaspoon', 'Teaspoons', 'teaspoon', 'teaspoons'), \
        'baseUnit', 'tsp', \
        'baseRatio', 1, \
        'fractions', arrayNew(2, 4, 8) \
    ) \
)
recipeUnits = objectKeys(recipeUnitInfo)


# Alternate ingredient unit map
recipeAlternateUnits = objectNew()
for recipeInfoUnit in recipeUnits:
    recipeInfo = objectGet(recipeUnitInfo, recipeInfoUnit)
    recipeInfoAlternates = objectGet(recipeInfo, 'alternates')
    if recipeInfoAlternates != null:
        for recipeAlternateUnit in recipeInfoAlternates:
            objectSet(recipeAlternateUnits, recipeAlternateUnit, recipeInfoUnit)
        endfor
    endif
endfor


# Ingredient text regular expression
recipeIngredientRegex = regexNew( \
    '^(?:\s*(?<whole>[1-9][0-9]*(?:\\.[0-9]*)?))?(?:\s*(?<numer>[1-9][0-9]*)\s*/\s*(?<denom>[1-9][0-9]*))?' + \
    '(?:\s*(?<unit>' + arrayJoin(recipeUnits, '|') + '|' + arrayJoin(objectKeys(recipeAlternateUnits), '|') + '))?' + \
    '\s+(?!/)(?<name>.+?)\s*$' \
)


# Scale and render an ingredient model to Markdown text
function recipeIngredientText(ingredient, scale):
    ingredientName = objectGet(ingredient, 'name')
    ingredientAmount = objectGet(ingredient, 'amount')
    ingredientUnit = objectGet(ingredient, 'unit')
    ingredientUnitInfo = objectGet(recipeUnitInfo, ingredientUnit)
    ingredientBaseUnit = objectGet(ingredientUnitInfo, 'baseUnit')
    ingredientBaseRatio = objectGet(ingredientUnitInfo, 'baseRatio')

    # Ingredient unit fuzz ratio
    unitFuzz = 0.05

    # Compute the scaled ingredient amount in base units
    amountBase = ingredientAmount * scale * ingredientBaseRatio

    # Try to converting to each known unit to find the best conversion
    bestIngredientUnit = ingredientUnit
    bestIngredientAmount = ingredientAmount * scale
    bestIngredientAmountNumerator = 0
    bestIngredientDiff = null
    for unit in recipeUnits:
        info = objectGet(recipeUnitInfo, unit)

        # Skip if not the same base unit
        if objectGet(info, 'baseUnit') != ingredientBaseUnit:
            continue
        endif

        # If scale is 1, skip unless these are the given units
        if scale == 1 && unit != ingredientUnit:
            continue
        endif

        # Match a unit fraction
        amountUnit = amountBase / objectGet(info, 'baseRatio')
        amountUnitInteger = mathFloor(amountUnit)
        amountUnitFraction = amountUnit - amountUnitInteger
        denominators = objectGet(info, 'fractions')
        for denominator in denominators:
            # Compute the closest numerator
            numerator = mathRound(amountUnitFraction * denominator)
            amountFractionInteger = if(numerator != denominator, amountUnitInteger, amountUnitInteger + 1)
            amountFractionNumerator = if(numerator != denominator, numerator, 0)
            amountFraction = amountFractionInteger + (amountFractionNumerator / denominator)
            if (amountFraction == 0):
                continue
            endif

            # Is the fraction close enough (in base units)?
            diff = mathAbs(amountFraction - amountUnit) * ingredientBaseRatio
            if diff / amountBase > unitFuzz:
                continue
            endif

            # Best conversion?
            if bestIngredientDiff != null && diff >= bestIngredientDiff:
                continue
            endif

            bestIngredientUnit = unit
            bestIngredientAmount = amountFractionInteger
            bestIngredientAmountNumerator = amountFractionNumerator
            bestIngredientAmountDenominator = denominator
            bestIngredientDiff = diff
        endfor
    endfor

    # Create the ingredient text
    bestIngredientInfo = objectGet(recipeUnitInfo, bestIngredientUnit)
    unitStr = if(objectHas(bestIngredientInfo, 'display'), objectGet(bestIngredientInfo, 'display'), bestIngredientUnit)
    if bestIngredientAmountNumerator == 0:
        amountStr = numberToFixed(bestIngredientAmount, 2, true)
    elif bestIngredientAmount == 0:
        amountStr = bestIngredientAmountNumerator + '/' + bestIngredientAmountDenominator
    else:
        amountStr = bestIngredientAmount + ' ' + bestIngredientAmountNumerator + '/' + bestIngredientAmountDenominator
    endif
    return '&nbsp;&nbsp;&nbsp;' + amountStr + if(unitStr != '', '&nbsp;&nbsp;' + unitStr, '') + '&nbsp;&nbsp;' + ingredientName
endfunction


function recipeTitlePage(model):
    # Set the recipe title page global model
    systemGlobalSet(recipeTitlePageGlobal, schemaValidate(recipeTitlePageTypes, 'RecipeTitlePage', model))

    # Render the title page
    recipeTitlePageRender()

    # Set the title page resize handler
    windowSetResize(recipeTitlePageRender)
endfunction


recipeTitlePageGlobal = 'recipeTitlePageModel'


recipeTitlePageTypes = schemaParse( \
    'struct RecipeTitlePage', \
    '    string title', \
    '    optional string title2', \
    '    optional string subtitle', \
    '    optional string imageURL', \
    '    optional string imageClickURL', \
    '    optional string sourceURL', \
    '    optional string color' \
)


function recipeTitlePageRender():
    model = systemGlobalGet(recipeTitlePageGlobal)
    title = objectGet(model, 'title')
    title2 = objectGet(model, 'title2')
    subtitle = objectGet(model, 'subtitle')
    imageURL = objectGet(model, 'imageURL')
    imageClickURL = objectGet(model, 'imageClickURL')
    sourceURL = objectGet(model, 'sourceURL')
    color = objectGet(model, 'color')

    # Determine the title image size
    fontSizePx = documentFontSize()
    margin = 1.5 * fontSizePx
    width = windowWidth() - 2 * margin
    height = windowHeight() - 2 * margin - 4 * fontSizePx

    # Compute the title text size
    titleLineSpacing = height / (8 - if(title2 == null, 1, 0) - if(subtitle == null, 1.5, 0) - if(imageURL == null, 2, 0))
    maxTitleFontSizePx = titleLineSpacing
    maxTitleLine = if(stringLength(title) > stringLength(title2), title, title2)
    titleFontSizePx = mathMin(maxTitleFontSizePx, drawTextHeight(maxTitleLine, 0.9 * width))
    subtitleFontSizePx = 0.7 * titleFontSizePx
    imageHeight = 2 * titleFontSizePx
    imageWidth = 4 * imageHeight
    titleY = 1.5 * titleLineSpacing
    titleY2 = titleY + if(title2 != null, titleLineSpacing, 0)
    subtitleY = titleY2 + if(subtitle != null, 1.5 * titleLineSpacing, 0)
    imageY = subtitleY + if(imageURL != null, 2 * titleLineSpacing - 0.5 * imageHeight, 0)

    # Render the title page
    documentSetTitle(if(title2 != null, title + ' ' + title2, title))
    recipeMenu()
    drawNew(width, height)
    drawStyle('none', 1, if(color != null, color, 'lightblue'))
    drawRect(0, 0, width, height)
    drawTextStyle(titleFontSizePx, 'black', true)
    drawText(title, 0.5 * width, titleY)
    if title2 != null:
        drawText(title2, 0.5 * width, titleY2)
    endif
    drawTextStyle(subtitleFontSizePx, 'black', true, true)
    if subtitle != null:
        drawText(subtitle, 0.5 * width, subtitleY)
    endif
    if imageURL != null:
        drawImage(0.5 * width - 0.5 * imageWidth, imageY, imageWidth, imageHeight, imageURL)
        if imageClickURL != null:
            drawOnClick(recipeTitleOnImageClick)
        endif
    endif
    if sourceURL != null:
        markdownPrint('[Source Code](' + sourceURL + ')')
    endif
endfunction


function recipeTitleOnImageClick():
    model = systemGlobalGet(recipeTitlePageGlobal)
    imageClickURL = objectGet(model, 'imageClickURL')
    windowSetLocation(imageClickURL)
endfunction
