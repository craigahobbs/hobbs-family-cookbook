# Licensed under the MIT License
# https://github.com/craigahobbs/hobbs-family-cookbook/blob/main/LICENSE


#
# Markdown Book scalable recipe library
#


# Recipe scaling min/max
recipeMinScale = 0.125
recipeMaxScale = 8


# Get the recipe scale factor (default is 1)
function recipeScale()
    return if(vScale != null, max(recipeMinScale, min(recipeMaxScale, vScale)), 1)
endfunction


# Render the recipe page's top-menu
function recipeMenu()
    markdownPrint('[Index](#url=&' + if(vCategory != null, "var.vCategory='" + encodeURIComponent(vCategory) + "'", 'var=') + ')')
endfunction


# Render the recipe page's header
function recipeHeader(author, servings)
    scale = recipeScale()
    halfScale = 0.5 * scale
    doubleScale = 2 * scale
    if(author != null, markdownPrint('', 'Author: ' + markdownEncode(author)))
    category = if(vCategory != null, "var.vCategory='" + encodeURIComponent(vCategory) + "'&", '')
    markdownPrint( \
        '', 'Scale: ' + round(scale, 3) + fromCharCode(160, 160) + \
        if(halfScale >= recipeMinScale, '([Halve](#' + category + 'var.vScale=' + round(halfScale, 3) + ') | ', '(Halve | '), \
        if(doubleScale <= recipeMaxScale, '[Double](#' + category + 'var.vScale=' + round(doubleScale, 3) + '))', 'Double)') \
    )
    if(servings != null, markdownPrint('', 'Servings: ' + scale * servings))
endfunction


# Parse, scale, and render the recipe ingredient text
function recipeIngredients(i1, i2, i3, i4, i5, i6, i7, i8, i9, i10, i11, i12, i13, i14, i15, i16, i17, i18, i19, i20)
    # Render the ingredients
    ingredients = arrayNew(i1, i2, i3, i4, i5, i6, i7, i8, i9, i10, i11, i12, i13, i14, i15, i16, i17, i18, i19, i20)
    scale = recipeScale()
    ixIngredient = 0
    ingredientLoop:
        ingredient = arrayGet(ingredients, ixIngredient)
        jumpif (ingredient == null) ingredientSkip
            ingredientModel = recipeParseIngredient(ingredient)
            if(ingredientModel == null, debugLog('Invalid ingredient text "' + ingredient + '"'))
            if(ingredientModel != null, markdownPrint('', recipeIngredientText(ingredientModel, scale)))
        ingredientSkip:
        ixIngredient = ixIngredient + 1
    jumpif (ixIngredient < arrayLength(ingredients)) ingredientLoop
endfunction


# Parse ingredient text to an ingredient model
function recipeParseIngredient(ingredient)
    match = regexMatch(recipeIngredientRegex, ingredient)
    groups = if(match != null, objectGet(match, 'groups', null), null)
    whole = if(match != null && objectGet(groups, 'whole') != null,  parseFloat(objectGet(groups, 'whole')), 0)
    numer = if(match != null && objectGet(groups, 'numer') != null, parseInt(objectGet(groups, 'numer')), 0)
    denom = if(match != null && objectGet(groups, 'denom') != null, parseInt(objectGet(groups, 'denom')), 1)
    unit = if(match != null && objectGet(groups, 'unit') != null, objectGet(groups, 'unit'), null)
    unit = if(unit == null, 'count', if(objectGet(recipeAlternateUnits, unit) != null, objectGet(recipeAlternateUnits, unit), unit))
    name = if(match != null, objectGet(groups, 'name'), null)
    jumpif ((whole != 0 || numer != 0) && name != null) isIngredient
        return null
    isIngredient:
    return objectNew( \
        'amount', whole + numer / denom, \
        'unit', unit, \
        'name', name \
    )
endfunction


# Ingredient unit info map
recipeUnitInfo = objectNew( \
    'count', objectNew( \
        'baseUnit', 'count', \
        'display', '', \
        'baseRatio', 1, \
        'fractions', arrayNew(2, 4, 8) \
    ), \
    'cup', objectNew( \
        'alternates', arrayNew('C', 'Cup', 'Cups', 'c', 'cups'), \
        'display', 'C', \
        'baseUnit', 'tsp', \
        'baseRatio', 48, \
        'fractions', arrayNew(2, 3, 4, 8) \
    ), \
    'lb', objectNew( \
        'alternates', arrayNew('Lb', 'Lbs', 'Pound', 'Pounds', 'lbs', 'pound', 'pounds'), \
        'baseUnit', 'oz', \
        'baseRatio', 16, \
        'fractions', arrayNew(2, 4, 8) \
    ), \
    'oz', objectNew( \
        'alternates', arrayNew('Oz', 'Ounce', 'Ounces', 'ounce', 'ounces'), \
        'baseUnit', 'oz', \
        'baseRatio', 1, \
        'fractions', arrayNew(2, 4, 8) \
    ), \
    'pinch', objectNew( \
        'baseUnit', 'pinch', \
        'baseRatio', 1, \
        'fractions', arrayNew(1) \
    ), \
    'tbsp', objectNew( \
        'alternates', arrayNew('T', 'Tbsp', 'Tablespoon', 'Tablespoons', 'tablespoon', 'tablespoons'), \
        'baseUnit', 'tsp', \
        'baseRatio', 3, \
        'fractions', arrayNew(1, 2) \
    ), \
    'tsp', objectNew( \
        'alternates', arrayNew('Tsp', 'Teaspoon', 'Teaspoons', 'teaspoon', 'teaspoons'), \
        'baseUnit', 'tsp', \
        'baseRatio', 1, \
        'fractions', arrayNew(2, 4, 8) \
    ) \
)
recipeUnits = objectKeys(recipeUnitInfo)


# Alternate ingredient unit map
recipeAlternateUnits = objectNew()
recipeIxInfo = 0
recipeInfoLoop:
    recipeInfoUnit = arrayGet(recipeUnits, recipeIxInfo)
    recipeInfo = objectGet(recipeUnitInfo, recipeInfoUnit)
    recipeInfoAlternates = objectGet(recipeInfo, 'alternates')
    jumpif (recipeInfoAlternates == null) recipeUnitNoAlternates
        recipeIxInfoAlternate = 0
        recipeInfoAlternateLoop:
            recipeAlternateUnit = arrayGet(recipeInfoAlternates, recipeIxInfoAlternate)
            objectSet(recipeAlternateUnits, recipeAlternateUnit, recipeInfoUnit)
            recipeIxInfoAlternate = recipeIxInfoAlternate + 1
        jumpif (recipeIxInfoAlternate < arrayLength(recipeInfoAlternates)) recipeInfoAlternateLoop
    recipeUnitNoAlternates:
    recipeIxInfo = recipeIxInfo + 1
jumpif (recipeIxInfo < arrayLength(recipeUnits)) recipeInfoLoop


# Ingredient text regular expression
recipeIngredientRegex = regexNew( \
    '^(?:\\s*(?<whole>[1-9][0-9]*(?:\\.[0-9]*)?))?(?:\\s*(?<numer>[1-9][0-9]*)\\s*/\\s*(?<denom>[1-9][0-9]*))?' + \
    '(?:\\s*(?<unit>' + arrayJoin(recipeUnits, '|') + '|' + arrayJoin(objectKeys(recipeAlternateUnits), '|') + '))?' + \
    '\\s+(?!/)(?<name>.+?)\\s*$' \
)


# Scale and render an ingredient model to Markdown text
function recipeIngredientText(ingredient, scale)
    ingredientName = objectGet(ingredient, 'name')
    ingredientAmount = objectGet(ingredient, 'amount')
    ingredientUnit = objectGet(ingredient, 'unit')
    ingredientUnitInfo = objectGet(recipeUnitInfo, ingredientUnit)
    ingredientBaseUnit = objectGet(ingredientUnitInfo, 'baseUnit')
    ingredientBaseRatio = objectGet(ingredientUnitInfo, 'baseRatio')

    # Ingredient unit fuzz ratio
    unitFuzz = 0.05

    # Compute the scaled ingredient amount in base units
    amountBase = ingredientAmount * scale * ingredientBaseRatio

    # Try to converting to each known unit to find the best conversion
    bestIngredientUnit = ingredientUnit
    bestIngredientAmount = ingredientAmount * scale
    bestIngredientAmountNumerator = 0
    bestIngredientDiff = null
    ixUnit = 0
    unitLoop:
        unit = arrayGet(recipeUnits, ixUnit)
        info = objectGet(recipeUnitInfo, unit)

        # Skip if not the same base unit
        jumpif (objectGet(info, 'baseUnit') != ingredientBaseUnit) unitDone

        # If scale is 1, skip unless these are the given units
        jumpif (scale == 1 && unit != ingredientUnit) unitDone

        # Match a unit fraction
        amountUnit = amountBase / objectGet(info, 'baseRatio')
        amountUnitInteger = floor(amountUnit)
        amountUnitFraction = amountUnit - amountUnitInteger
        denominators = objectGet(info, 'fractions')
        ixDenominator = 0
        denominatorLoop:
            denominator = arrayGet(denominators, ixDenominator)

            # Compute the closest numerator
            numerator = round(amountUnitFraction * denominator)
            amountFractionInteger = if(numerator != denominator, amountUnitInteger, amountUnitInteger + 1)
            amountFractionNumerator = if(numerator != denominator, numerator, 0)
            amountFraction = amountFractionInteger + (amountFractionNumerator / denominator)
            jumpif (amountFraction == 0) denominatorDone

            # Is the fraction close enough (in base units)?
            diff = abs(amountFraction - amountUnit) * ingredientBaseRatio
            jumpif (diff / amountBase > unitFuzz) denominatorDone

            # Best conversion?
            jumpif (bestIngredientDiff != null && diff >= bestIngredientDiff) denominatorDone

            bestIngredientUnit = unit
            bestIngredientAmount = amountFractionInteger
            bestIngredientAmountNumerator = amountFractionNumerator
            bestIngredientAmountDenominator = denominator
            bestIngredientDiff = diff

            denominatorDone:
            ixDenominator = ixDenominator + 1
        jumpif (ixDenominator < arrayLength(denominators)) denominatorLoop

        unitDone:
        ixUnit = ixUnit + 1
    jumpif (ixUnit < arrayLength(recipeUnits)) unitLoop

    # Create the ingredient text
    bestIngredientInfo = objectGet(recipeUnitInfo, bestIngredientUnit)
    unitStr = if(objectGet(bestIngredientInfo, 'display') != null, objectGet(bestIngredientInfo, 'display'), bestIngredientUnit)
    jumpif (bestIngredientAmountNumerator != 0) nonZeroNumerator
        amountStr = bestIngredientAmount + ''
        jump ingredientDone
    nonZeroNumerator:
    jumpif (bestIngredientAmount != 0) nonZeroAmount
        amountStr = bestIngredientAmountNumerator + '/' + bestIngredientAmountDenominator
        jump ingredientDone
    nonZeroAmount:
        amountStr = bestIngredientAmount + ' ' + bestIngredientAmountNumerator + '/' + bestIngredientAmountDenominator
    ingredientDone:

    return fromCharCode(160, 160, 160) + amountStr + if(unitStr != '', fromCharCode(160, 160) + unitStr, '') + \
        fromCharCode(160, 160) + ingredientName
endfunction
