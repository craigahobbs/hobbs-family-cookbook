# Licensed under the MIT License
# https://github.com/craigahobbs/hobbs-family-cookbook/blob/main/LICENSE


#
# The Markdown Book application
#


# The Markdown Book application main entry point
async function markdownBook(bookURL)
    # Load and validate the Markdown Book file
    bookURL = if(bookURL != null, bookURL, 'book.json')
    book = schemaValidate(bookTypes, 'MarkdownBook', fetch(bookURL))

    # Render the page
    if vDoc then
        markdownBookDoc()
    else if vSearch != null then
        markdownBookSearch(book)
    else then
        markdownBookIndex(book)
    endif
endfunction


# Render the Markdown Book index
async function markdownBookIndex(book)
    # Set the page title
    title = objectGet(book, 'title')
    titleURL = objectGet(book, 'titleURL')
    setDocumentTitle(title)
    markdownPrint( \
        '[Title](#url=' + encodeURIComponent(titleURL) + '&var=)', \
        " | [Search](#var.vSearch='')", \
        '', \
        '# ' + markdownEscape(title) \
    )

    # Render the category links
    foreach category in objectGet(book, 'categories') do
        categoryTitle = objectGet(category, 'title')
        isSelected = (vCategory == categoryTitle)

        # Render the category title link
        categoryTitleURL = if(isSelected, '#var=', "#var.vCategory='" + encodeURIComponent(categoryTitle) + "'")
        markdownPrint('', '## [' + markdownEscape(categoryTitle) + '](' + categoryTitleURL + ')')

        # If this is the selected category, fetch the category files and render the file links
        if isSelected then
            categoryURLs = objectGet(category, 'files')
            categoryTexts = fetch(categoryURLs, null, true)
            foreach categoryURL, ixFile in categoryURLs do
                categoryText = arrayGet(categoryTexts, ixFile)
                fileTitle = markdownTitle(markdownParse(categoryText))
                linkTitle = if(fileTitle != null, fileTitle, categoryURL)
                markdownPrint('', '&nbsp;[' + markdownEscape(linkTitle) + '](#url=' + \
                    encodeURIComponent(categoryURL) + "&var.vCategory='" + encodeURIComponent(categoryTitle) + "')")
            endforeach
        endif
    endforeach
endfunction


# Render the Markdown Book file format documentation
function markdownBookDoc()
    setDocumentTitle('MarkdownBook')
    elementModelRender(schemaElements(bookTypes, 'MarkdownBook'))
endfunction


# Render the Markdown Book search page
async function markdownBookSearch(book)
    # Set the page title
    title = objectGet(book, 'title')
    setDocumentTitle(title)
    markdownPrint( \
        '[Index](#var=)', \
        '# Search' \
    )

    # Render the search form
    elementModelRender(arrayNew( \
        objectNew( \
            'html', 'input', \
            'attr', objectNew( \
                'autocomplete', 'off', \
                'id', 'search-text', \
                'style', 'font-size: inherit; border: thin solid black; padding: 0.4em;', \
                'type', 'text', \
                'value', vSearch \
            ), \
            'callback', objectNew('keyup', markdownBookSearchOnKeyup) \
        ), \
        objectNew('text', stringFromCharCode(160, 160)), \
        objectNew( \
            'html', 'a', \
            'attr', objectNew('style', 'cursor: pointer; user-select: none;'), \
            'elem', objectNew('text', 'Search'), \
            'callback', objectNew('click', markdownBookSearchOnClick) \
        ) \
    ))
    setDocumentFocus('search-text')

    # Compute the search results and render
    if vSearch != '' then
        markdownPrint('## Results')
        results = markdownBookSearchResults(book, vSearch)
        if arrayLength(results) != 0 then
            foreach result in results do
                resultURL = objectGet(result, 'url')
                resultCategory = objectGet(result, 'category')
                resultTitle = objectGet(result, 'title')
                markdownPrint('', '[' + markdownEscape(resultTitle) + \
                    '](#url=' + encodeURIComponent(resultURL) + "&var.vCategory='" + encodeURIComponent(resultCategory) + "')")
            endforeach
        else then
            markdownPrint('No matching pages found')
        endif
    endif
endfunction


function markdownBookSearchOnClick()
    searchText = getDocumentInputValue('search-text')
    setWindowLocation("#var.vSearch='" + encodeURIComponent(searchText) + "'")
endfunction


function markdownBookSearchOnKeyup(keyCode)
    if keyCode == 13 then
        markdownBookSearchOnClick()
    endif
endfunction


async function markdownBookSearchResults(book, text)
    # Create the search text regex
    words = stringSplit(text, regexNew('\s+'))
    wordsEscaped = arrayNew()
    minSearchWordLength = 3
    foreach word in words do
        if stringLength(word) >= minSearchWordLength then
            arrayPush(wordsEscaped, regexEscape(word))
        endif
    endforeach
    if arrayLength(wordsEscaped) == 0 then
        return arrayNew()
    endif
    reSearch = regexNew('\b(?:' + arrayJoin(wordsEscaped, '|') + ')', 'ig')

    # Compute the list of pages (url, category)
    pages = arrayNew()
    pageURLs = arrayNew()
    foreach category in objectGet(book, 'categories') do
        foreach fileURL in objectGet(category, 'files') do
            arrayPush(pages, objectNew('url', fileURL, 'category', objectGet(category, 'title')))
            arrayPush(pageURLs, fileURL)
        endforeach
    endforeach

    # Load all pages and search each page's text
    results = arrayNew()
    pageTexts = fetch(pageURLs, null, true)
    foreach page, ixPage in pages do
        pageText = arrayGet(pageTexts, ixPage)
        if pageText != null then
            # Add the result page title
            pageTitle = markdownTitle(markdownParse(pageText))
            objectSet(page, 'title', pageTitle)

            # Add the page match
            matches = regexMatchAll(reSearch, pageText)
            matchCount = arrayLength(matches)
            if matchCount then
                objectSet(page, 'matches', matchCount)
                arrayPush(results, page)
            endif
        endif
    endforeach

    # Return sorted list of matching pages (url, title, category, matches)
    return arraySort(results, markdownBookSearchSort)
endfunction


function markdownBookSearchSort(a, b)
    aMatches = -objectGet(a, 'matches')
    bMatches = -objectGet(b, 'matches')
    compareMatches = if(aMatches < bMatches, -1, if(aMatches == bMatches, 0, 1))
    if compareMatches != 0 then
        return compareMatches
    endif

    aTitle = objectGet(a, 'title')
    bTitle = objectGet(b, 'title')
    return if(aTitle < bTitle, -1, if(aTitle == bTitle, 0, 1))
endfunction


# The Markdown Book file format schema
bookTypes = schemaParse( \
    '# The Markdown Book JSON file format', \
    'struct MarkdownBook', \
    '    # The book title', \
    '    string title', \
    '', \
    "    # The markdown book's title page markdown file relative URL", \
    '    optional string(len > 0) titleURL', \
    '', \
    "    # The markdown book's categories", \
    '    MarkdownBookCategory[len > 0] categories', \
    '', \
    '', \
    '# The Markdown Book category struct', \
    'struct MarkdownBookCategory', \
    '', \
    '    # The category title', \
    '    string(len > 0) title', \
    '', \
    "    # The category's markdown file relative URLs", \
    '    string(len > 0)[len > 0] files' \
)
