# Licensed under the MIT License
# https://github.com/craigahobbs/hobbs-family-cookbook/blob/main/LICENSE

include <forms.mds>


#
# The Markdown Book application
#


# The Markdown Book application main entry point
async function markdownBook(bookURL):
    # Load and validate the Markdown Book file
    bookURL = if(bookURL != null, bookURL, 'book.json')
    book = schemaValidate(bookTypes, 'MarkdownBook', systemFetch(bookURL))

    # Render the page
    if vDoc:
        markdownBookDoc()
    elif vSearch != null:
        markdownBookSearch(book)
    else:
        markdownBookIndex(book)
    endif
endfunction


# Render the Markdown Book index
async function markdownBookIndex(book):
    # Set the page title
    title = objectGet(book, 'title')
    titleURL = objectGet(book, 'titleURL')
    documentSetTitle(title)
    markdownPrint( \
        '[Title](#url=' + urlEncodeComponent(titleURL) + '&var=)', \
        " | [Search](#var.vSearch='')", \
        '', \
        '# ' + markdownEscape(title) \
    )

    # Render the category links
    for category in objectGet(book, 'categories'):
        categoryTitle = objectGet(category, 'title')
        isSelected = (vCategory == categoryTitle)

        # Render the category title link
        categoryTitleURL = if(isSelected, '#var=', "#var.vCategory='" + urlEncodeComponent(categoryTitle) + "'")
        markdownPrint('', '## [' + markdownEscape(categoryTitle) + '](' + categoryTitleURL + ')')

        # If this is the selected category, fetch the category files and render the file links
        if isSelected:
            categoryURLs = objectGet(category, 'files')
            categoryTexts = systemFetch(categoryURLs, null, true)
            for categoryURL, ixFile in categoryURLs:
                categoryText = arrayGet(categoryTexts, ixFile)
                fileTitle = markdownTitle(markdownParse(categoryText))
                linkTitle = if(fileTitle != null, fileTitle, categoryURL)
                markdownPrint('', '&nbsp;[' + markdownEscape(linkTitle) + '](#url=' + \
                    urlEncodeComponent(categoryURL) + "&var.vCategory='" + urlEncodeComponent(categoryTitle) + "')")
            endfor
        endif
    endfor
endfunction


# Render the Markdown Book file format documentation
function markdownBookDoc():
    documentSetTitle('MarkdownBook')
    elementModelRender(schemaElements(bookTypes, 'MarkdownBook'))
endfunction


# Render the Markdown Book search page
async function markdownBookSearch(book):
    # Set the page title
    title = objectGet(book, 'title')
    documentSetTitle(title)
    markdownPrint( \
        '[Index](#var=)', \
        '# Search' \
    )

    # Render the search form
    elementModelRender(arrayNew( \
        objectNew('html', 'p', 'elem', formsTextElements('search-text', vSearch, 32, markdownBookSearchOnClick)), \
        objectNew('html', 'p', 'elem', formsLinkButtonElements('Search', markdownBookSearchOnClick)) \
    ))
    documentSetFocus('search-text')

    # Compute the search results and render
    if vSearch != '':
        markdownPrint('## Results')
        results = markdownBookSearchResults(book, vSearch)
        if arrayLength(results) != 0:
            for result in results:
                resultURL = objectGet(result, 'url')
                resultCategory = objectGet(result, 'category')
                resultTitle = objectGet(result, 'title')
                markdownPrint('', '[' + markdownEscape(resultTitle) + \
                    '](#url=' + urlEncodeComponent(resultURL) + "&var.vCategory='" + urlEncodeComponent(resultCategory) + "')")
            endfor
        else:
            markdownPrint('No matching pages found')
        endif
    endif
endfunction


function markdownBookSearchOnClick():
    searchText = documentInputValue('search-text')
    windowSetLocation("#var.vSearch='" + urlEncodeComponent(searchText) + "'")
endfunction


async function markdownBookSearchResults(book, text):
    # Create the search text regex
    words = stringSplit(text, regexNew('\s+'))
    wordsEscaped = arrayNew()
    minSearchWordLength = 3
    for word in words:
        if stringLength(word) >= minSearchWordLength:
            arrayPush(wordsEscaped, regexEscape(word))
        endif
    endfor
    if arrayLength(wordsEscaped) == 0:
        return arrayNew()
    endif
    reSearch = regexNew('\b(?:' + arrayJoin(wordsEscaped, '|') + ')', 'ig')

    # Compute the list of pages (url, category)
    pages = arrayNew()
    pageURLs = arrayNew()
    for category in objectGet(book, 'categories'):
        for fileURL in objectGet(category, 'files'):
            arrayPush(pages, objectNew('url', fileURL, 'category', objectGet(category, 'title')))
            arrayPush(pageURLs, fileURL)
        endfor
    endfor

    # Load all pages and search each page's text
    results = arrayNew()
    pageTexts = systemFetch(pageURLs, null, true)
    for page, ixPage in pages:
        pageText = arrayGet(pageTexts, ixPage)
        if pageText != null:
            # Add the result page title
            pageTitle = markdownTitle(markdownParse(pageText))
            objectSet(page, 'title', pageTitle)

            # Add the page match
            matches = regexMatchAll(reSearch, pageText)
            matchCount = arrayLength(matches)
            if matchCount:
                objectSet(page, 'matches', matchCount)
                arrayPush(results, page)
            endif
        endif
    endfor

    # Return sorted list of matching pages (url, title, category, matches)
    return arraySort(results, markdownBookSearchSort)
endfunction


function markdownBookSearchSort(a, b):
    aMatches = -objectGet(a, 'matches')
    bMatches = -objectGet(b, 'matches')
    compareMatches = if(aMatches < bMatches, -1, if(aMatches == bMatches, 0, 1))
    if compareMatches != 0:
        return compareMatches
    endif

    aTitle = objectGet(a, 'title')
    bTitle = objectGet(b, 'title')
    return if(aTitle < bTitle, -1, if(aTitle == bTitle, 0, 1))
endfunction


# The Markdown Book file format schema
bookTypes = schemaParse( \
    '# The Markdown Book JSON file format', \
    'struct MarkdownBook', \
    '    # The book title', \
    '    string title', \
    '', \
    "    # The markdown book's title page markdown file relative URL", \
    '    optional string(len > 0) titleURL', \
    '', \
    "    # The markdown book's categories", \
    '    MarkdownBookCategory[len > 0] categories', \
    '', \
    '', \
    '# The Markdown Book category struct', \
    'struct MarkdownBookCategory', \
    '', \
    '    # The category title', \
    '    string(len > 0) title', \
    '', \
    "    # The category's markdown file relative URLs", \
    '    string(len > 0)[len > 0] files' \
)
